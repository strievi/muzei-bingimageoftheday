apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "de.devmil.muzei.bingimageofthedayartsource"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 8
        versionName "1.7"

        archivesBaseName = "muzei_biot-v${defaultConfig.versionName}-${buildTime()}"

        multiDexEnabled true

    }
    signingConfigs {
        release {
            def propertiesFile = file('../Key/keystore.properties')
            if(propertiesFile.exists()) {
                println "Keystore properties found! Building signed..."
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(propertiesFile))
                storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                println "No Keystore properties found! Building unsigned..."
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            def propertiesFile = file('../Key/keystore.properties')
            if(propertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    applicationVariants.all { variant ->
        def bingImageOfTheDayAuthorityValue = "${applicationId}.provider"
        def bingImageOfTheDayFileProviderAuthorityValue = "${applicationId}.fileprovider"

        def mergedFlavor = variant.getMergedFlavor()
        mergedFlavor.manifestPlaceholders = [bingImageOfTheDayAuthority: bingImageOfTheDayAuthorityValue,
                                             bingImageOfTheDayFileProviderAuthority: bingImageOfTheDayFileProviderAuthorityValue]

        variant.buildConfigField "String", "BING_IMAGE_OF_THE_DAY_AUTHORITY", "\"${bingImageOfTheDayAuthorityValue}\""
        variant.buildConfigField "String", "BING_IMAGE_OF_THE_DAY_FILE_PROVIDER_AUTHORITY", "\"${bingImageOfTheDayFileProviderAuthorityValue}\""
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.0-alpha02'
    implementation "androidx.core:core-ktx:$rootProject.ext.ktxVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.coroutinesVersion"
    implementation "android.arch.work:work-runtime-ktx:$rootProject.ext.workManagerVersion"
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:multidex:1.0.3'
}
repositories {
    mavenCentral()
}

def buildTime() {
    return new Date().format("yyyyMMdd")
}

buildscript {
}
