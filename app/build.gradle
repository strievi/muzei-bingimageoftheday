apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.strievi.muzei.bingimageofthedayartsource"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 8
        versionName "1.7"

        archivesBaseName = "muzei-bingimageoftheday-v${defaultConfig.versionName}"

        multiDexEnabled true

    }
    signingConfigs {
        release {
            def propertiesFile = file('../Key/keystore.properties')
            if(propertiesFile.exists()) {
                println "Keystore properties found! Building signed..."
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(propertiesFile))
                storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                println "No Keystore properties found! Building unsigned..."
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            def propertiesFile = file('../Key/keystore.properties')
            if(propertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    applicationVariants.all { variant ->
        def bingImageOfTheDayAuthorityValue = "${applicationId}.provider"
        def bingImageOfTheDayContentProviderAuthorityValue = "${applicationId}.contentprovider"

        def mergedFlavor = variant.getMergedFlavor()
        mergedFlavor.manifestPlaceholders = [bingImageOfTheDayAuthority: bingImageOfTheDayAuthorityValue,
                                             bingImageOfTheDayContentProviderAuthority: bingImageOfTheDayContentProviderAuthorityValue]

        variant.buildConfigField "String", "BING_IMAGE_OF_THE_DAY_AUTHORITY", "\"${bingImageOfTheDayAuthorityValue}\""
        variant.buildConfigField "String", "BING_IMAGE_OF_THE_DAY_CONTENT_PROVIDER_AUTHORITY", "\"${bingImageOfTheDayContentProviderAuthorityValue}\""
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.1'
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.work:work-runtime-ktx:$workManagerVersion"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'androidx.multidex:multidex:2.0.1'
}
repositories {
    mavenCentral()
}

buildscript {
}
